require conf/machine/include/qcom-common.inc
require conf/machine/include/arm/arch-armv8a.inc

MACHINE_FEATURES += "screen ext2 efi"

# UFS partitions in 820/845/RB5 setup with 4096 logical sector size
EXTRA_IMAGECMD:ext4 += " -b 4096 "

UBOOT_CONFIG ??= "qcs6490-rb3gen2 sdm845-db845c"
UBOOT_CONFIG[qcs6490-rb3gen2] = "qcm6490_defconfig"
UBOOT_CONFIG[sdm845-db845c] = "qcom_defconfig"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"

# Support for dragonboard{410, 820, 845}c, rb5
KERNEL_IMAGETYPE ?= "Image.gz"

# UKI generation needs uncompressed Kernel image
KERNEL_IMAGETYPES = "Image"

# To build an Android boot image
KERNEL_CLASSES += "linux-qcom-bootimg"

# For dtb.bin generation
KERNEL_CLASSES += "linux-qcom-dtbbin"

SERIAL_CONSOLE ?= "115200 ttyMSM0"
KERNEL_DEVICETREE ?= " \
    qcom/apq8016-sbc.dtb \
    qcom/apq8096-db820c.dtb \
    qcom/sdm845-db845c.dtb \
    qcom/qcs404-evb-4000.dtb \
    qcom/qrb2210-rb1.dtb \
    qcom/qrb4210-rb2.dtb \
    qcom/qrb5165-rb5.dtb \
    qcom/sa8775p-ride.dtb \
    qcom/sm8450-hdk.dtb \
"

# These DT are not upstreamed (yet) and currenty exist only as a patches against linux-yocto
KERNEL_DEVICETREE:append:pn-linux-yocto = " \
    qcom/qcm6490-idp.dtb \
    qcom/qcs6490-rb3gen2.dtb \
"

# These DT are part of linux-yocto-dev
KERNEL_DEVICETREE:append:pn-linux-yocto-dev = " \
    qcom/qcm6490-idp.dtb \
    qcom/qcs615-ride.dtb \
    qcom/qcs6490-rb3gen2.dtb \
    qcom/qcs6490-rb3gen2-vision-mezzanine.dtb \
    qcom/qcs8300-ride.dtb \
    qcom/qcs9075-iq-9075-evk.dtb \
    qcom/qcs9100-ride.dtb \
    qcom/qcs9100-ride-r3.dtb \
"

QCOM_BOOTIMG_PAGE_SIZE[apq8016-sbc] ?= "2048"
QCOM_BOOTIMG_ROOTFS ?= "/dev/sda1"
QCOM_BOOTIMG_ROOTFS[apq8016-sbc] ?= "/dev/mmcblk0p14"
QCOM_BOOTIMG_ROOTFS[sm8450-hdk] ?= "PARTLABEL=userdata"
QCOM_BOOTIMG_ROOTFS[qcs404-evb-4000] ?= "/dev/mmcblk0p27"
QCOM_BOOTIMG_ROOTFS[qrb2210-rb1] ?= "PARTLABEL=rootfs"
QCOM_BOOTIMG_ROOTFS[qrb4210-rb2] ?= "PARTLABEL=rootfs"
QCOM_BOOTIMG_ROOTFS[qcs6490-rb3gen2] ?= "PARTLABEL=system"
SD_QCOM_BOOTIMG_ROOTFS[apq8016-sbc] ?= "/dev/mmcblk1p7"
KERNEL_CMDLINE_EXTRA[sdm845-db845c] ?= "clk_ignore_unused pd_ignore_unused"

# Userspace tools
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    packagegroup-qcom-boot-essential \
"

MACHINE_EXTRA_RRECOMMENDS += " \
    packagegroup-qcom-boot-additional \
"

# Modules and firmware for all supported machines
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    kernel-modules \
    ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'wireless-regdb-static', '', d)} \
    packagegroup-dragonboard410c-firmware \
    packagegroup-dragonboard820c-firmware \
    packagegroup-dragonboard845c-firmware \
    packagegroup-rb1-firmware \
    packagegroup-rb2-firmware \
    packagegroup-rb3gen2-firmware \
    packagegroup-rb5-firmware \
"
